buildscript {
    repositories {
        maven {
            name 'glide-snapshot'
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            name 'fabric'
            url 'https://maven.fabric.io/public'
        }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.29.0'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.google.firebase.crashlytics'

def keystorePropertiesFile = project.file("release.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyAliasPassword']
            storeFile file(keystoreProperties['keyStore'])
            storePassword keystoreProperties['keyStorePassword']
        }
    }
    buildFeatures {
        dataBinding = true
    }
    compileSdkVersion 31
    defaultConfig {
        applicationId "org.gophillygo.app"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 26
        versionName "1.4.1"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
        release {
            crunchPngs false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
    }
    lint {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Firebase
    implementation 'com.google.firebase:firebase-crashlytics:17.1.1'
    implementation 'com.google.firebase:firebase-analytics:17.4.4'

    // multidex
    implementation 'com.android.support:multidex:2.0.1'

    // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Java8 support for Lifecycles
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'

    // Room
    implementation 'androidx.room:room-runtime:2.2.5'
    annotationProcessor 'androidx.room:room-compiler:2.2.5'

    // Android UI libraries
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.preference:preference:1.1.1"

    // Android Play services libraries
    implementation "com.google.android.gms:play-services-location:17.0.0"
    implementation "com.google.android.gms:play-services-maps:17.0.0"

    // Android WorkManager
    implementation 'androidx.work:work-runtime:2.7.1'

    // Carousel
    implementation 'com.synnapps:carouselview:0.1.5'

    // Badges
    implementation 'com.github.nekocode:Badge:2.1'

    // Glide
    implementation('com.github.bumptech.glide:glide:4.11.0') {
        exclude group: "com.android.support"
    }
    implementation("com.github.bumptech.glide:recyclerview-integration:4.11.0") {
        transitive = false
    }
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    // Gson converter for Retrofit
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Okio log interceptor
    implementation 'com.squareup.okhttp3:logging-interceptor:4.7.2'

    // Dagger 2
    def daggerVersion = '2.35.1'
    implementation "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    // OSS licensing
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    // testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Test helpers for LiveData
    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    // Test helpers for Room
    testImplementation 'androidx.room:room-testing:2.2.5'
}

